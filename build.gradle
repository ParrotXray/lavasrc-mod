plugins {
    id "org.jetbrains.kotlin.jvm" version "2.0.21" apply false
    id "org.jetbrains.kotlin.plugin.serialization" version "2.0.21" apply false
}

allprojects {
    group = "com.github.topi314.lavasrc"
    
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://maven.arbjerg.dev/releases" }
        maven { url "https://maven.arbjerg.dev/snapshots" }
        maven { url "https://jitpack.io" }
    }
}

def getGitVersion() {
    def versionStr = new ByteArrayOutputStream()
    def result = exec {
        standardOutput versionStr
        errorOutput versionStr
        ignoreExitValue true
        commandLine "git", "describe", "--exact-match", "--tags"
    }
    if (result.exitValue == 0) {
        return versionStr.toString().trim()
    }

    versionStr = new ByteArrayOutputStream()
    exec {
        standardOutput versionStr
        errorOutput versionStr
        commandLine "git", "rev-parse", "--short", "HEAD"
    }
    return versionStr.toString().trim()
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"
    
    version getGitVersion()
    
    compileJava.options.encoding = "UTF-8"
}